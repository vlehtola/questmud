string dir, file, code, link_room, desc, door_label;
int open, locked;

set_locked(arg) { locked = arg; }

query_door_locked() { return locked; }

set_open(arg) { open = arg; }

query_door_open() { return open; }

random_door_code() {
    string str;
    int i;
    str = "";
    while(i < 3) {
      str = str + random(10);
      i += 1;
    }
    code = str;
    if (link_room) { call_other(link_room, "set_door_code", code); }
    locked = 1;
}

set_door_code(str) { code = str; }

query_door_code() { return code; }

set_door_link(str) { link_room = str; }

set_door_desc(str) { desc = str; }

set_door_label(str) { door_label = str; }

door_label() {
  if (door_label) { return "labelled '"+door_label+"' "; }
  return "";
}

query_door_desc() {
  if (desc) { return desc; }
  return "";
}

door_long_desc() {
  write("This");
  if (desc) write(" "+desc);
  write(" door ");
  if (door_label) { write("labelled '"+door_label+"' "); }
  if (locked) { write("is locked.\n"); }
  if (open) { write("is open.\n"); }
  else { write("is closed.\n"); }
  write("It leads to the "+dir+".\n");
  write("Available door commands:\n");
  write("  open door\n");
  write("  close door\n");
  write("  lock door with key\n");
  write("  unlock door with key\n");
}


door_init() {
  add_action("open", "open");
  add_action("close", "close");
  add_action("lock", "lock");
  add_action("unlock", "unlock");
/*  add_action("move_by_door", dir); */
}

set_door_dir(str, path) {
  dir = str;  
  file = path;
}

query_door_dir() { return dir; }

query_door_file() { return file; }

/*
move_by_door() {
  string str;
  if (!open) { return; }
  str = dir+"#"+file;
  this_player()->move_player(str);
  return 1;
}
*/

open(str) {
  if (str != "door") { return 0; }
  if (open) { write("The door is already open.\n"); return 1; }
  if (locked) { write("The door is locked.\n"); return 1; }
  write("You open the door.\n");
  open = 1;
  if (link_room) call_other(link_room, "door_opened");
  add_exit(dir, file);
  return 1;
}

door_opened() {
  tell_room(this_object(), "The door opens.\n");
  open = 1;
  add_exit(dir,file);
  return 1;
}

close(str) {
  if (str != "door") { return 0; }
  if (!open) { write("The door is already closed.\n"); return 1; }
  write("You close the door.\n");
  open = 0;
  if (link_room) call_other(link_room, "door_closed");
  remove_exit(dir);
  return 1;
}

door_closed() {
  tell_room(this_object(), "The door is closed.\n");
  open = 0;
  remove_exit(dir);
  return 1;
}

lock(arg) {
  string str;
  object ob;
  if (!arg) { return; }
  if (!sscanf(arg, "door with %s", str)) { 
        write("Lock door with what?\n");    
        return 1; 
  }
  ob = present(str, this_player());
  if (!ob) { write("You don't have a '"+str+"'.\n"); return 1; }
  if (!ob->query_key_code()) { write("That is not a key.\n"); return 1; }
  if (ob->query_key_code() != query_door_code()) {
        write("The key does not fit.\n");
        return 1;
  }
  if (locked) { write("The door is already locked.\n"); return 1; }
  write("You lock the door.\n");
  locked = 1;
  if (link_room) call_other(link_room, "door_locked");
  return 1;
}

door_locked() {
  tell_room(this_object(), "You hear someone locking the door.\n");
  locked = 1;
  return 1;
}

unlock(arg) {
  string str;
  object ob;
  if (!arg) { return; }
  if (!sscanf(arg, "door with %s", str)) { 
        write("Unlock door with what?\n");    
        return 1; 
  }
  ob = present(str, this_player());
  if (!ob) { write("You don't have a '"+str+"'.\n"); return 1; }
  if (!ob->query_key_code()) { write("That is not a key.\n"); return 1; }
  if (ob->query_key_code() != query_door_code()) {
        write("The key does not fit.\n");
        return 1;
  }
  if (!locked) { write("The door is not locked.\n"); return 1; }
  write("You unlock the door.\n");
  locked = 0;
  if (link_room) call_other(link_room, "door_locked");
  return 1;
}

door_unlocked() {
  tell_room(this_object(), "You hear someone unlocking the door.\n");
  locked = 0;
  return 1;
}
